import com.vanniktech.maven.publish.SonatypeHost

plugins {
    id 'java'
    id "com.vanniktech.maven.publish" version "0.28.0"
    id 'signing'
}

def latestGitTag = 'git describe --tags --abbrev=0'.execute().text.trim().replace('v', '')

group = 'com.smunity'
version = latestGitTag ?: '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

signing {
    def signingKey = System.getenv("ORG_GRADLE_PROJECT_signingKey")
    def signingPassword = System.getenv("ORG_GRADLE_PROJECT_signingPassword")
    if (signingKey && signingPassword) {
        useInMemoryPgpKeys(signingKey, signingPassword)
    } else {
        useGpgCmd()
    }
    sign publishing.publications
}

mavenPublishing {
    publishToMavenCentral(SonatypeHost.CENTRAL_PORTAL)

    coordinates('kr.co.smunity', 'sangmyung-univ-auth', version)

    pom {
        name = 'sangmyung-univ-auth'
        description = 'Sangmyung University Student Authentication Library'
        url = 'https://github.com/smu-nity/sangmyung-univ-auth'

        licenses {
            license {
                name = 'MIT License'
                url = 'https://github.com/smu-nity/sangmyung-univ-auth/blob/main/LICENSE'
            }
        }

        developers {
            developer {
                id = 'hyunmin0317'
                name = 'Hyunmin Choi'
                email = 'hyunmin-choi@naver.com'
            }
        }

        scm {
            connection = "scm:git:git://github.com/smu-nity/sangmyung-univ-auth.git"
            developerConnection = "scm:git:ssh://github.com/smu-nity/sangmyung-univ-auth.git"
            url = "https://github.com/smu-nity/sangmyung-univ-auth"
        }
    }
}

dependencies {
    // HTML Parsing and JSON Processing
    implementation 'org.jsoup:jsoup:1.17.2'
    implementation 'org.json:json:20240303'

    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.34'
    annotationProcessor 'org.projectlombok:lombok:1.18.34'

    // Testing
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.3'
}

test {
    useJUnitPlatform()
}
